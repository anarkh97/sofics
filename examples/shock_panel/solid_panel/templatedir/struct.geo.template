// Unit: mm 

// GMsh command: gmsh -3 -format msh -o fluid3d.msh fluid3d.geo

// INPUTS
dx = 0.5;        // mesh size
L  = 50;         // length of the beam
L2 = 0.5*L;      // half length
w  = 1;          // half through plane thickness
tt = {cdv_1};    // thickness of tip
tm = {cdv_2};    // thickness of mid-chord
tr = {cdv_3};    // thickness of root

nn = Floor(L2/dx) + 1; // number of nodes.

// x coordinates
x1 = 0.5*(tt - tr);
x2 = 0.5*(tt - tm);
x3 = 0.0;
x4 = 0.5*(tt + tr);
x5 = 0.5*(tt + tm);
x6 = tt;

// beam --- points
Point(1) = { x1,   0.0, -w, dx};
Point(2) = { x2,    L2, -w, dx};
Point(3) = { x3,     L, -w, dx}; 
Point(4) = { x4,   0.0, -w, dx}; 
Point(5) = { x5,    L2, -w, dx};
Point(6) = { x6,     L, -w, dx};

// beam --- lines
Line(1) = {1, 2};
Line(2) = {2, 3};
Line(3) = {3, 6};
Line(4) = {5, 6};
Line(5) = {4, 5};
Line(6) = {1, 4};

// beam --- line loop
Line Loop(1) = {1, 2, 3, -4, -5, -6};

// beam --- bottom surface
Plane Surface(1) = {1};

// combine triangles to quads
Transfinite Curve{1} = nn;
Transfinite Curve{2} = nn;
Transfinite Curve{3} = 3;
Transfinite Curve{4} = nn;
Transfinite Curve{5} = nn;
Transfinite Curve{6} = 3;
Transfinite Surface{1} = {1, 3, 6, 4};
Recombine Surface{1};

// extrude
Ex[] = Extrude {0, 0,2*w} {
  Surface{1};
  Layers{1};
  Recombine;
};

// symmetry surfaces (bottom and top)
Physical Surface(1) = {1, Ex[0]};

// fixed surface
Physical Surface(2) = {Ex[7]};

// wetted surfaces (FS interface)
Physical Surface(3) = {Ex[2], Ex[3], Ex[4], Ex[5], Ex[6]};

// volume
Physical Volume(1) = {Ex[1]}; 

// Smoothing the final mesh
Mesh.Smoothing = 20;
Mesh.SmoothRatio = 3;
